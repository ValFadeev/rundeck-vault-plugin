plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}
ext.rundeckVersion='3.0.2-20180803'
defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'pl.allegro.tech.build.axion-release'
sourceCompatibility = 1.8
ext.rundeckPluginVersion= '1.2'

/**
 * Set this to a comma-separated list of full classnames of your implemented Rundeck
 * plugins.
 */
ext.pluginClassNames='io.github.valfadeev.rundeck.plugin.vault.VaultStoragePlugin'

scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

dependencies {
    // add any third-party jar dependencies you wish to include in the plugin
    // using the `pluginLibs` configuration as shown here:

    pluginLibs group: 'com.bettercloud', name: 'vault-java-driver', version: '4.0.0', ext: 'jar'


    //the compile dependency won't add the rundeck-core jar to the plugin contents
    compile group: 'org.rundeck', name: 'rundeck-core', version: rundeckVersion
    testCompile(
            [group: 'junit', name: 'junit', version: '4.12', ext: 'jar'],
            [group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3', ext: 'jar'],
            [group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3', ext: 'jar'],
            [group: 'org.mockito', name: 'mockito-core', version: '4.0.0', ext: 'jar'],
            [group: 'net.bytebuddy', name: 'byte-buddy', version: '1.12.1'],
            [group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.12.1'],
            [group: 'org.objenesis', name: 'objenesis', version: '3.2']
    )

}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : 'Vault Storage'
        attributes 'Rundeck-Plugin-Description' : 'This is a Storage Backend plugin for storing Key Store data in Vault.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,storage,vault'
        attributes 'Rundeck-Plugin-License': 'MIT'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/vault-storage'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Main-Class': "io.github.valfadeev.rundeck.plugin.vault.VaultStoragePlugin"
        attributes 'Class-Path': "${libList} lib/rundeck-core-${rundeckVersion}.jar"
    }
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
